plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        myPublicationName(MavenPublication) {
            from components.java

            pom {
                name = 'utils'
                description = 'A Java utils library'
                url = 'https://github.com/tortitast/java-utils'
            
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'tortitast'
                        name = 'Tortitas'
                        email = 'victorgf2011@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/tortitast/java-utils.git'
                    developerConnection = 'scm:git:ssh://github.com/tortitast/java-utils.git'
                    url = 'https://github.com/tortitast/java-utils'
                }
            }
        }   
    }

    repositories {
        maven {
            name = "MavenCentral"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getProperty("ossrhUsername")
                password = System.getProperty("ossrhPassword")
            }
        }
    }
}

println "Remember to set the following environment variables: systemProp.ossrhUsername, systemProp.ossrhPassword, signing.keyId, signing.password, signing.secretKeyRingFile"
println "ossrhUsername: " + System.getProperty("ossrhUsername")
// println "ossrhPassword: " + System.getProperty("ossrhPassword")

java {
    withJavadocJar()
    withSourcesJar()
}

signing {
    sign publishing.publications.myPublicationName
}

archivesBaseName = 'utils'
group = 'eu.tortitas.utils'
version System.getenv("GITHUB_TAG") ?: "1.3.1"

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit test framework.
    testImplementation 'junit:junit:4.13.2'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:31.1-jre'

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation 'org.xerial:sqlite-jdbc:3.41.2.1'
}

task run(type: JavaExec) {
    main = 'utils.App'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}
